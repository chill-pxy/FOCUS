#version 450

layout(binding = 1) uniform sampler2D texSampler;

layout(location = 0) in vec3 fragColor;
layout(location = 1) in vec2 fragTexCoord;
layout(location = 2) in vec3 fragPosition;
layout(location = 3) in vec3 fragNormal;
layout(location = 4) in vec3 fragLightPosition;
layout(location = 5) in vec3 fragViewPosition;
layout(location = 6) in vec3 fragLightColor;
layout(location = 7) in float fragLightStrength;

layout(location = 0) out vec4 outColor;

void main()
{
    vec3 color = texture(texSampler, fragTexCoord).rgb;
    
    // compute light direction
    vec3 lightDir = normalize(fragLightPosition - fragPosition);
    
    // compute view direction
    vec3 viewDir = normalize(fragViewPosition - fragPosition);
    
    // compute distance
    float distance = length(fragLightPosition - fragPosition);
    float attenuation = 1.0 / (distance * distance) * fragLightStrength;

    // compute normal
    vec3 norm = normalize(fragNormal);

    // ambient
    float ambientStrength = 0.05;
    vec3 ambient = ambientStrength * color  * fragLightColor * attenuation;
    
    // diffuse
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = diff * color  * fragLightColor * attenuation;
    
    // specular
    vec3 halfwayDir = normalize(lightDir + viewDir);
    float spec = pow(max(dot(norm, halfwayDir), 0.0), 32); // 这里的32是光泽度指数
    vec3 specular = spec * fragLightColor * 0.3 * attenuation;
    
    // output
    outColor = vec4(ambient + diffuse + specular, 1.0);
}